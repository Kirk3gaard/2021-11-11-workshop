(input_seqnames,) = glob_wildcards("data/{filename}.faa")


rule all:
    input:
        expand(
        "results/{name}.{aligner}_aligned.fasttree_{model}.tre",
        name = input_seqnames,
        aligner = ["mafft", "muscle"],
        model = ["gtr", "lg"]
        )


rule mafft:
    input:
        "data/{seqname}.faa"
    output:
        "intermediate/{seqname}.mafft_aligned.faa"
    threads: 2
    shell:
        "module load mafft && "
        "mafft --thread {threads} {input} > {output}"


rule muscle:
    input:
        "data/{seqname}.faa"
    output:
        "intermediate/{seqname}.muscle_aligned.faa"
    shell:
        "module load muscle && "
        "muscle -in {input} -out {output}"


rule fasttree:
    input:
        "intermediate/{seqname}.{aligner}_aligned.faa"
    output:
        "results/{seqname}.{aligner}_aligned.fasttree_{model}.tre"
    log:
        "log/{seqname}.{aligner}_aligned.fasttree_{model}.log"
    shell:
        "module load fasttree && "
        "fasttree -{wildcards.model} -log {log} {input} > {output}"